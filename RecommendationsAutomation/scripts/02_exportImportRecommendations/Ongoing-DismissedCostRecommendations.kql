AdvisorResources
| where type =~ 'microsoft.advisor/recommendations'
| where properties.category == 'Cost'
| extend
    recommendationTypeId = tostring(properties.recommendationTypeId),
    category = tostring(properties.category), 
    impact = tostring(properties.impact), 
    problem = tostring(properties.shortDescription.problem),
    annualSavingsAmount = tostring(properties.extendedProperties.annualSavingsAmount), 
    savingsCurrency = tostring(properties.extendedProperties.savingsCurrency),
    lookbackPeriod = tostring(properties.extendedProperties.lookbackPeriod), 
    savingsTerm = tostring(properties.extendedProperties.term),
    lastUpdated = tostring(properties.lastUpdated),
    region = tostring(properties.extendedProperties.region),
    ResourceId = tolower(tostring(coalesce(properties.resourceMetadata.resourceId,''))),
    SubscriptionId = subscriptionId
| where lookbackPeriod == '7' and savingsTerm =='P1Y'
| join kind=leftouter (
    resourcecontainers
    | where type == 'microsoft.resources/subscriptions'
    | project subscriptionId, subscriptionName = name
) on subscriptionId
| join kind=leftouter (
    resources
    | project ResourceId = tolower(id), ResourceName = name
) on ResourceId
| join kind=leftouter (
    AdvisorResources
    | where type =~ 'microsoft.advisor/recommendations'
    | where properties.category == 'Cost'
    | extend 
        dis_id = tolower(id),
        dis_recommendationTypeId = tostring(properties.recommendationTypeId),
        dis_ResourceId = tolower(tostring(coalesce(properties.resourceMetadata.resourceId,''))) 
    | project dis_SusbscriptionId = subscriptionId, dis_id,dis_recommendationTypeId,dis_ResourceId
    | join kind=inner ( 
        AdvisorResources
        | where type =~ 'microsoft.advisor/suppressions'
        | extend s_RecommendationId = tolower(substring(tostring(id), 0, indexof(tostring(id), "/suppressions")))
        | project s_RecommendationId
    ) on $left.dis_id == $right.s_RecommendationId
)   on $left.recommendationTypeId == $right.dis_recommendationTypeId and $left.SubscriptionId == $right.dis_SusbscriptionId  and $left.ResourceId == $right.dis_ResourceId
| extend status = case (
                        isnull(dis_recommendationTypeId),"NEW",
                        "DISMISSED"
                    )
| project recommendationTypeId, category,impact,problem, annualSavingsAmount, savingsCurrency,lookbackPeriod,savingsTerm, lastUpdated,region, ResourceId, SubscriptionId,id,status,subscriptionName,ResourceName